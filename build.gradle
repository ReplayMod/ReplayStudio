buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/staging'
        }
    }

    dependencies {
        classpath 'ch.raffael.gradlePlugins.preshadow:gradle-preshadow-plugin:1.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

defaultTasks 'clean', 'build', 'shadowJar', 'install'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'
apply plugin: 'maven'
apply from: 'gradle/preshadow.gradle'

// Methods to determine detailed version string
def gitDescribe() {
    try {
        // determine git-describe output
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty=*'
            standardOutput = out
        }
        return out.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

group = 'com.replaymod'
description = 'ReplayStudio'
version = gitDescribe()
mainClassName = 'com.replaymod.replaystudio.launcher.Launcher'

ext.projectName = 'ReplayStudio'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'
ext.inceptionYear = '2016'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.viaversion.com'
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    preshadow 'com.github.replaymod:MCProtocolLib:d4e4037'
    preshadow 'us.myles:viaversion:1.1.2-SNAPSHOT'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.google.guava:guava:21.0'
    compile "org.projectlombok:lombok:1.16.6"
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.guava:guava-testlib:18.0'
    testCompile 'pl.pragmatists:JUnitParams:1.0.4'
}

license {
    ext.name = project.projectName
    ext.author = project.author
    ext.url = project.authorUrl
    ext.year = project.inceptionYear
    header new File(project.getProjectDir(), 'HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    exclude 'LICENSE.txt'
    exclude 'META-INF/*'
}

preshadowJar {
    relocate 'io.netty', 'com.github.steveice10.netty'
}

shadowJar {
    append('META-INF/NOTICE.txt')
    from(tasks.findByName('preshadowJar').outputs.files.collect {it.isDirectory() ? it : zipTree(it)})
}

eclipse.project {
        name = 'ReplayStudio'
}

jar.manifest.mainAttributes(
        'Main-Class': mainClassName,
        'Implementation-Title': description,
        'Implementation-Version': gitDescribe()
)
