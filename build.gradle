buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url 'https://oss.sonatype.org/content/groups/staging'
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'ch.raffael.gradlePlugins.preshadow:gradle-preshadow-plugin:1.0'
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.0'
        classpath 'com.github.johni0702:gradle-reproducible-builds-plugin:3dbf20d'
    }
}

defaultTasks 'publishToMavenLocal'

apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: 'ch.raffael.preshadow'
apply plugin: 'de.johni0702.reproducible-builds'

def gitCommitHash() {
    try {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = out
        }
        return out.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

group = 'com.replaymod.replaystudio'
description = 'ReplayStudio'
version = gitCommitHash()
mainClassName = 'com.replaymod.replaystudio.launcher.Launcher'

ext.projectName = 'ReplayStudio'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(8))

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.viaversion.com'
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    preshadow 'com.github.replaymod.viaversion:viaversion-common:027362504'

    preshadow 'com.google.guava:guava:17.0'
    implementation 'com.google.code.gson:gson:2.3.1'

    implementation 'it.unimi.dsi:fastutil:8.3.1' // this is the version MC ships with 1.14.4, upgrade with care

    implementation 'com.github.viaversion:opennbt:0a02214' // 2.0-SNAPSHOT (ViaVersion Edition)
    preshadow 'com.github.steveice10:packetlib:614d56cdc0' // 1.3

    implementation 'org.apache.commons:commons-lang3:3.3.2'
    implementation 'org.apache.commons:commons-collections4:4.0'
    implementation 'commons-cli:commons-cli:1.2'
    compileOnly(annotationProcessor("org.projectlombok:lombok:1.16.6"))
    testImplementation 'junit:junit:4.11'
    testImplementation 'com.google.guava:guava-testlib:18.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.0.4'
}

preshadowJar {
    relocate 'io.netty', 'com.github.steveice10.netty'
    relocate 'us.myles.ViaVersion.api', 'com.replaymod.replaystudio.lib.viaversion.legacyapi'
    relocate 'com.viaversion.viaversion', 'com.replaymod.replaystudio.lib.viaversion'
    relocate 'com.google.common', 'com.replaymod.replaystudio.lib.guava'
}

shadowJar {
    archiveClassifier.set("full")
    append('META-INF/NOTICE.txt')
    from(tasks.findByName('preshadowJar').outputs.files.collect {it.isDirectory() ? it : zipTree(it)})
}
build.dependsOn shadowJar

jar.manifest.mainAttributes(
        'Main-Class': mainClassName,
        'Implementation-Title': description,
        'Implementation-Version': gitCommitHash()
)

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
