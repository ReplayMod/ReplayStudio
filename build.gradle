buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/staging'
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'ch.raffael.gradlePlugins.preshadow:gradle-preshadow-plugin:1.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.github.johni0702:gradle-reproducible-builds-plugin:3dbf20d'
    }
}

defaultTasks 'clean', 'build', 'shadowJar', 'install'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'
apply plugin: 'ch.raffael.preshadow'
apply plugin: 'de.johni0702.reproducible-builds'

def gitCommitHash() {
    try {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = out
        }
        return out.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

group = 'com.replaymod.replaystudio'
description = 'ReplayStudio'
version = gitCommitHash()
mainClassName = 'com.replaymod.replaystudio.launcher.Launcher'

ext.projectName = 'ReplayStudio'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.viaversion.com'
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    preshadow 'com.github.replaymod.viaversion:viaversion-common:bc943b6'

    // ViaVersion will use the relocated netty of PacketLib
    // un-shaded from ViaVersion
    compile 'com.google.guava:guava:17.0'
    compile 'com.google.code.gson:gson:2.3.1'

    compile 'com.github.steveice10:opennbt:d50274d' // 1.2
    preshadow 'com.github.steveice10:packetlib:614d56cdc0' // 1.3

    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-cli:commons-cli:1.2'
    compileOnly "org.projectlombok:lombok:1.16.6"
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.guava:guava-testlib:18.0'
    testCompile 'pl.pragmatists:JUnitParams:1.0.4'
}

preshadowJar {
    relocate 'io.netty', 'com.github.steveice10.netty'
    relocate 'us.myles.ViaVersion', 'com.replaymod.replaystudio.us.myles.ViaVersion'

    // un-shading from ViaVersion (see dependencies)
    exclude 'com/google/gson/**' // gson
    exclude 'com/google/common/**' // guava
    exclude 'com/google/thirdparty/publicsuffix/**' // also part of guava
    // don't need this one, ViaVersion uses it to parse configs (which we never need to do)
    exclude 'org/yaml/snakeyaml/**' // snakeyaml
}

shadowJar {
    archiveName = "${baseName}-${version}.full.${extension}"
    append('META-INF/NOTICE.txt')
    from(tasks.findByName('preshadowJar').outputs.files.collect {it.isDirectory() ? it : zipTree(it)})
}
build.dependsOn shadowJar

jar.manifest.mainAttributes(
        'Main-Class': mainClassName,
        'Implementation-Title': description,
        'Implementation-Version': gitCommitHash()
)

